configure(allprojects) { project ->
	group = "com.impaqgroup.recruit-me"
	
	ext.commonsLangVersion = "3.1"
	ext.cglibVersion = "2.2.2"
	ext.groovyVersion = "2.1.1"
	ext.guavaVersion = "14.0.1"
	ext.hamcrestVersion = "1.3"
	ext.objenesisVersion = "1.3"
	ext.spockVersion = "0.7-groovy-2.0"
}

configure(subprojects) { subproject ->
	apply plugin: "groovy"
	
	compileJava {
		sourceCompatibility=1.7
		targetCompatibility=1.7
	}
	
	compileTestJava {
		sourceCompatibility=1.7
		targetCompatibility=1.7
	}
	
	dependencies {
		// common compile dependencies
		compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
		
		// common test dependencies
		testCompile("cglib:cglib-nodep:${cglibVersion}")
		testCompile("org.hamcrest:hamcrest-core:${hamcrestVersion}")
		testCompile("org.objenesis:objenesis:${hamcrestVersion}")
		testCompile("org.spockframework:spock-core:${spockVersion}")
	}
	
	repositories {
		mavenCentral()
	}
}

task wrapper(type: Wrapper) {
	description = "Generates gradlew[.bat] scripts"
	gradleVersion = '1.5'
	
	doLast() {
		def gradleOpts = "-XX:MaxPermSize=1024m -Xmx1024m"
		def gradleBatOpts = "$gradleOpts -XX:MaxHeapSize=256m"
		File wrapperFile = file("gradlew")
		wrapperFile.text = wrapperFile.text.replace("DEFAULT_JVM_OPTS=",
			"GRADLE_OPTS=\"$gradleOpts \$GRADLE_OPTS\"\nDEFAULT_JVM_OPTS=")
		File wrapperBatFile = file("gradlew.bat")
		wrapperBatFile.text = wrapperBatFile.text.replace("set DEFAULT_JVM_OPTS=",
			"set GRADLE_OPTS=$gradleBatOpts %GRADLE_OPTS%\nset DEFAULT_JVM_OPTS=")
	}
}
